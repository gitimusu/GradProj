// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    apply from: project.file("buildscript.gradle")
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.0'
        classpath 'com.google.gms:google-services:3.1.0'
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}
//apply plugin: "catkin"

allprojects {
    group "org.ros.android_core"
//    version = project.catkin.pkg.version
    repositories {
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

configure(subprojects.findAll { it.name.startsWith("android_") }) {
    apply plugin: "ros-android"
    afterEvaluate { project ->
        // Change the layout of Android projects to be compatible with Eclipse.
        android {
            sourceSets {
                //noinspection GroovyAssignabilityCheck
                main {
                    manifest.srcFile "AndroidManifest.xml"
                    res.srcDirs "res"
                    assets.srcDirs "assets"
                    java.srcDirs "src"
                }
            }

            // Copy JAR dependencies into the libs directory for Eclipse.
            task deployLibs(type: Copy) {
                from { configurations.compile }
                into { "${project.projectDir}/libs" }
            }

            // Exclude a few files that are duplicated across our dependencies and
            // prevent packaging Android applications.
            packagingOptions {
                /* https://github.com/rosjava/android_core/issues/194 */
                exclude "META-INF/LICENSE.txt"
                exclude "META-INF/NOTICE.txt"
            }
        }
    }
}

defaultTasks 'assembleRelease', 'uploadArchives'